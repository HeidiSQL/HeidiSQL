; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; Original HeidiSQL Portable setup script for Innosetup based on
; HeidiSQL setup script

[Setup]
AppId=HeidiSQL
AppName=HeidiSQL Portable
AppVerName=HeidiSQL Portable 4.0
AppVersion=1.0.0.0
VersionInfoVersion=1.0.0.0
AppPublisher=Ansgar Becker
AppPublisherURL=http://www.heidisql.com/
AppSupportURL=http://forum.heidisql.com/
AppUpdatesURL=http://download.heidisql.com/
AppContact=heidisql@anse.de
DefaultDirName=\HeidiSQLPortable
LicenseFile=.\..\..\out\license.txt
WizardImageFile=.\installer-logo-portable.bmp
WizardImageBackColor=$ffffff
WizardSmallImageFile=.\..\..\res\installer-small-logo.bmp
OutputDir=.
OutputBaseFilename=HeidiSQLPortable_4.0_Setup
SetupIconFile=.\..\..\res\mainicon.ico
; uncomment the following line if you want your installation to run on NT 3.51 too.
; MinVersion=4,3.51
UsePreviousAppDir=no
DirExistsWarning=auto
PrivilegesRequired=none
; no uninstallation support is included, requiring the end-user
; to manually remove the files pertaining to your application.
Uninstallable=no

[InstallDelete]
Type: files; Name: "{app}\App\HeidiSQL\libmysql40.dll"
Type: files; Name: "{app}\App\HeidiSQL\libmysql41.dll"
Type: files; Name: "{app}\App\HeidiSQL\heidisql.exe.manifest"

[Files]
Source: ".\HeidiSQLPortable.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\..\..\out\heidisql.exe"; DestDir: "{app}\App\HeidiSQL"; Flags: ignoreversion
Source: ".\..\..\out\readme.txt"; DestDir: "{app}\App\HeidiSQL"; Flags: ignoreversion
Source: ".\..\..\out\license.txt"; DestDir: "{app}\App\HeidiSQL"; Flags: ignoreversion
Source: ".\..\..\out\gpl.txt"; DestDir: "{app}\App\HeidiSQL"; Flags: ignoreversion
Source: ".\..\..\out\libmysql.dll"; DestDir: "{app}\App\HeidiSQL"; Flags: ignoreversion

[INI]
Filename: "{app}\App\HeidiSQL\heidisql.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.heidisql.com/"
Filename: "{app}\App\HeidiSQL\heidisql_forum.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.heidisql.com/forum/"
Filename: "{app}\App\HeidiSQL\donate.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.heidisql.com/donate.php"

[Run]
Filename: "{app}\HeidiSQLPortable.exe"; Description: "Launch HeidiSQL Portable"; Flags: nowait postinstall skipifsilent

[Code]
procedure DonateClick(Sender: TObject);
var
  ErrorCode: Integer;
begin
  ShellExec('open', 'http://www.heidisql.com/donate.php', '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
end;

procedure InitializeWizard();
var
  txt: TNewStaticText;
  btn: TButton;
begin
  txt := TNewStaticText.Create(WizardForm);
  txt.Parent := WizardForm.FinishedPage;
  txt.Caption := 'HeidiSQL is free software. You may make a donation:';
  txt.Left := WizardForm.FinishedLabel.Left;
  txt.Top := WizardForm.FinishedLabel.Top + 130;
  txt.AutoSize := True;
  
  btn := TButton.Create(WizardForm);
  btn.Parent := WizardForm.FinishedPage;
  btn.Left := txt.Left;
  btn.Top := txt.Top + txt.Height + 10;
  btn.Width := 120;
  btn.Height := WizardForm.CancelButton.Height;
  btn.Caption := 'Donate';
  btn.OnClick := @DonateClick;
end;
